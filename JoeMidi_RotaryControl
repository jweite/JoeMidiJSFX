/*******************************************************************************
*  Copyright 2017 Joseph Weitekamp                
*    12/2/17:  Added control message (1) control of rotary for PX3
*
*******************************************************************************/

desc: Control Rotary Speed on VB3 with low A & B keys

////////////////////////////////////////////////////////////////////////////////
@init

statNoteOn = $x90;
statNoteOff = $x80;
statControlChange = $xB0;
noteNumberLowA = $x15;
noteNumberLowB = notenumberLowA + 2;
cc1 = $x0001;
slow = $x0000;
fast = $x7F00;

////////////////////////////////////////////////////////////////////////////////
@slider

////////////////////////////////////////////////////////////////////////////////
@block

while
(
	midirecv(offset, msg1, msg23) ?
	(	
		// Extract message type and channel
		status = msg1 & $xF0;
		channel = msg1 & $x0F;
			
		// Is it a note event?
		status == statNoteOn ?
		(
			// Extract note value
			note = msg23 & $x7F;
			
			// Low A: set rotary slow
			note == noteNumberLowA ?
			(
				midisend(offset, statControlChange | channel, cc1 | slow );
			)
			:
			(
				// Low B: set rotary fast
				note == noteNumberLowB ?
				(
					midisend(offset, statControlChange | channel, cc1 | fast );
				)
				:
				(
					// All other notes: pass through
					midisend(offset, msg1, msg23);
				)
			)
		)
		:
		(
			// Is it a control event?
			status == statControlChange ?
			(
				// Extract control number
				ctlNum = msg23 & $x7F;
				
				// ctl1: control rotary
				ctlNum == 1 ?
				(
					// Extract control value
					ctlVal = (msg23/256)|0;
					
					// 0: Set Rotary Slow
					ctlVal == 0 ?
					(
						midisend(offset, statControlChange | channel, cc1 | slow );
					)
					:
					(
						// non-zero: Set Rotary Fast
						midisend(offset, statControlChange | channel, cc1 | fast );
					)
				)
				:
				(
					// All other controls: passthrough
					midisend(offset, msg1, msg23);
				)
			)
			:
			(
				// All other statuses: passthrough
				midisend(offset, msg1, msg23);
			)
		);
			
		1; // Force loop to continue until all messages have been processed
	);
);

